/*
 * Copyright (c) 2021-2022. CropDB author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'com.github.hauner.jarTest'
    id 'jacoco'
}

jar {
    archivesBaseName = 'cropdb-jackson-mapper'
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    api platform(project(':cropdb-bom'))
    api project(":cropdb")
    api "org.slf4j:slf4j-api"
    api "com.fasterxml.jackson.core:jackson-databind"

    annotationProcessor "org.projectlombok:lombok:1.18.22"

    testImplementation project(path: ':cropdb-mvstore-adapter', configuration: 'default')
    testAnnotationProcessor "org.projectlombok:lombok:1.18.20"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.locationtech.jts:jts-core:1.18.2"
    testImplementation "org.awaitility:awaitility:4.1.1"
    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:2.14.1"
    testImplementation "org.apache.logging.log4j:log4j-core:2.15.0"
    testImplementation "com.github.javafaker:javafaker:1.0.2"
    testImplementation platform("com.fasterxml.jackson:jackson-bom:2.13.0")
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
}

test {
    testLogging.showStandardStreams = false
    testLogging.exceptionFormat = 'full'
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'cropdb-jackson-mapper'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Crop Jackson Mapper'
                description = 'A Jackson based Crop mapper implementation.'
                url = 'https://github.com/misselvexu/mixmicro-cropdb'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'misselvexu'
                        name = 'Elve.Xu'
                        email = 'iskp.me@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:misselvexu/mixmicro-cropdb.git'
                    developerConnection = 'scm:git:git@github.com:misselvexu/mixmicro-cropdb.git'
                    url = 'git@github.com:misselvexu/mixmicro-cropdb.git'
                }
            }
        }
    }
    repositories {
        if (version.endsWith('SNAPSHOT') || project.hasProperty('release')) {
            maven {
                name = "OSSRH"
                def releasesRepoUrl = "http://nexus.hgj.net/repository/releases/"
                def snapshotsRepoUrl = "http://nexus.hgj.net/repository/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.getenv('MAVEN_USERNAME')
                    password System.getenv('MAVEN_PASSWORD')
                }

            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}